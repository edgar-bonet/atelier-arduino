---
layout: reveal
title: Atelier Arduino – Programmation efficace
description: >
    Le but de cet atelier est d'apprendre les principes de la
    programmation non bloquante (ou concurrente). Il s'agit de pouvoir
    gérer plusieurs capteur ou actionneurs en parallèle sans jamais
    bloquer le programme. On y aborde les notions de boucle d'évènements
    et de machine à états finie.
date: 2025-06-28
---
## Atelier Arduino – Programmation efficace

![](img/Arduino_uno_r3.jpg)  
<small>Arduino Uno R3 – © Maker Media GmbH – CC BY-SA 4.0</small>

Edgar Bonet

CCSTI Grenoble – 2025-06-28

<!--horizontal-->

## Présentation

<img src="img/avatar.jpeg" style="float: right">
* Edgar Bonet
  * physicien, spécialisé dans le magnétisme
  * libriste de longue date
  * contributeur fréquent à [Arduino Stackexchange][ase]
  * [contributeur occasionnel][pr] à l'écosystème Arduino
* [Laboratoire Ouvert Grenoblois][log]
  * hackerspace grenoblois
  * partage de connaissances et savoir-faire
* [La Casemate][ccsti]
  * premier CCSTI créé en France
  * expositions, rencontres, ateliers, FabLab...

[ase]: https://arduino.stackexchange.com/
[pr]: https://github.com/pulls?q=author%3Aedgar-bonet+is%3Apr+is%3Amerged+org%3Aadafruit+org%3Aarduino+org%3Aarduino-libraries
[log]: https://www.logre.eu/
[ccsti]: https://lacasemate.fr/

Note: Faire tour de table pour se présenter.  
Dire : attentes, expérience en programmation Arduino, évènementielle,
machines à états.

<!--horizontal-->

## Programmation non bloquante – Motivation

But : gérer plusieurs tâches en parallèle, sans qu'elles se bloquent
mutuellement.

![Arduino Stack Exchange](img/arduino-se.png) Recherche « blocking
is:answer » → 1 250 résultats

> Many of the programming questions on the Arduino forum can be answered
> with one simple response: Implement a "Finite State Machine."
> — Majenko

Note: Les questions ne sont pas posées en termes de parallélisme.

<!--vertical-->

### Stratégies pour gérer des tâches concurrentes

1. Un processus/thread par tâche
  * optimal pour tâches lourdes sur un processeur multi-cœur
  * nécessite un OS (ordonnanceur)
  * coût en mémoire (une pile par thread)
  * **complexité** : synchronisation
2. Gérer des évènements successif dans un thread unique
  * optimal quand la gestion d'un évènement est légère
  * faible coût en mémoire et CPU
  * **complexité** : machines à états
  * approche populaire pour le Web (Nginx, Node.js...)

Note: Les threads peuvent être plus simples, mais la synchronisation est
difficile à déboguer  
On demande à un Arduino des tâches simples (< 1 ms).  
Il passe le plus clair de son temps à attendre.

<!--horizontal-->

## Faire clignoter des LED

Une seule LED (File/Examples/01.Basics/Blink) :

```arduino
void setup() {
    pinMode(LED_BUILTIN, OUTPUT);
}

void loop() {
    digitalWrite(LED_BUILTIN, HIGH);
    delay(400);
    digitalWrite(LED_BUILTIN, LOW);
    delay(400);
}
```

Question : comment faire clignoter deux LEDs, une en triolets et une en
temps binaire ?

<!--vertical-->

### Clignotement non bloquant

Fichier → Exemples → 02.Digital → BlinkWithoutDelay

```arduino
int etat_LED = LOW;                          // quel est l'état actuel de LED ?
unsigned long temps_dernier_changement = 0;  // quand a-t-elle changé d'état pour la dernière fois ?

void setup() {
    pinMode(LED_BUILTIN, OUTPUT);
}

void loop() {
    unsigned long maintenant = millis();  // l'heure actuelle

    if (maintenant - temps_dernier_changement >= 400) {  // est-il temps de changer d'état ?
        temps_dernier_changement = maintenant;
        if (etat_LED == LOW) {  // calculer le nouvel état
            etat_LED = HIGH;
        } else {
            etat_LED = LOW;
        }
        digitalWrite(LED_BUILTIN, etat_LED);  // appliquer cet état
    }
}
```

Exercices :
* Charger et exécuter ce programme
* faire clignoter deux LEDs avec des périodes différentes

Note: **dans examples/**  
`temps_dernier_changement += 400;` possible aussi.  
Pour deux LED, dupliquer les variables globales.

<!--vertical-->

### Principe général d'un programme non-bloquant

```arduino
void loop() {
    if (évènement_machin_est_arrivé()) {
        gérer_évènement_machin();
    }

    if (évènement_bidule_est_arrivé()) {
        gérer_évènement_bidule();
    }

    if (évènement_truc_est_arrivé()) {
        gérer_évènement_truc();
    }

    // etc.
}
```

Note: Ne **jamais** s'arrêter à attendre.  
`loop()` tourne > 1 kHz.

<!--horizontal-->

## Lire un bouton poussoir

But : quand on appuie sur un bouton, le moteur tourne trois secondes.

```arduino
const int broche_bouton = 2;
const int broche_moteur = 4;

void setup() {
    pinMode(broche_bouton, INPUT);
    pinMode(broche_moteur, OUTPUT);
}

void loop() {
    // Attendre la pression sur le bouton.
    while (digitalRead(broche_bouton) == LOW) {
        /* attendre... */
    }

    // Allumer le moteur pendant trois secondes.
    digitalWrite(broche_moteur, HIGH);
    delay(3000);
    digitalWrite(broche_moteur, LOW);
}
```

Comment implémenter cette fonctionnalité tout en faisant clignoter la
LED ?

Note: La LED « heartbeat » témoigne que le programme ne s'est pas
bloqué.

<!--vertical-->

### Solution non-bloquante

```arduino
void loop() {
    static bool moteur_tourne = false;   // le moteur tourne-t-il ?
    static uint32_t temps_allumage = 0;  // quand a-t-il été allumé ?

    // Quand on appuie sur le bouton, allumer le moteur.
    if (!moteur_tourne && digitalRead(broche_bouton) == HIGH) {
        digitalWrite(broche_moteur, HIGH);
        moteur_tourne = true;
        temps_allumage = millis();
    }

    // Quand il a tourné trois secondes, éteindre le moteur.
    if (moteur_tourne && millis() - temps_allumage >= 3000) {
        digitalWrite(broche_moteur, LOW);
        moteur_tourne = false;
    }
}
```

Note: **dans examples/**  
signification de `static`  
L'évènement qu'on attend, et la réponse, dépendent de l'état du
système.

<!--vertical-->

### Notion d'automate fini

<div style="float: right">
![](img/bouton-moteur.png)
</div>

Le programme précédent peut s'interpréter comme un « automate fini » à
deux états :

* dans chaque état on attend un évènement
* les évènements déclenchent des transitions
* les actions de l'automate sont associées aux transitions

Note: automate fini = machine à états finie  
Différence avec organigramme (flowchart).

<!--horizontal-->

## Automates finis

Ma montre

d'après le mode d'emploi

![](img/montre-0.jpg)
<span style="display: inline-block; width: 5ex"></span>
![](img/montre-1.png)

Note: Un état peut attendre plusieurs évènements : le premier arrivé
déclenche la transition.

<!--vertical-->

La même montre,  
d'après mes essais.

![](img/montre-2.png)

Note: Les ingénieurs qui l'ont conçue on probablement dessiné un schéma
semblable à celui-ci.

<!--horizontal-->

## Exercice : feu tricolore

![](img/feu_tricolore.png)

```arduino
void loop() {
    digitalWrite(feu_vert, LOW);
    digitalWrite(feu_orange, HIGH);
    delay(1000);

    digitalWrite(feu_orange, LOW);
    digitalWrite(feu_rouge, HIGH);
    delay(5000);

    digitalWrite(feu_rouge, LOW);
    digitalWrite(feu_vert, HIGH);
    delay(4000);
}
```

<!--vertical-->

### Solution non-bloquante

```arduino
void loop() {
    static enum { VERT, ORANGE, ROUGE } etat = VERT;
    static uint32_t dernier_changement = 0;
    uint32_t maintenant = millis();
    uint32_t temps_ecoule = maintenant - dernier_changement;

    switch (etat) {
        case VERT:
            if (temps_ecoule >= duree_vert) {
                digitalWrite(feu_vert, LOW);
                digitalWrite(feu_orange, HIGH);
                etat = ORANGE;
                dernier_changement = maintenant;
            }
            break;
        case ORANGE:
            // ...
    }
}
```

Recette pour convertir un code bloquant en automate fini :

* repérer les endroits où le code est en attente d'un évènement
* faire de chaque attente un état

Note: **dans examples/**  
Structure switch/case utilisable pour tout automate fini.  
Comment ajouter un bouton d'appel piétons ?

<!--vertical-->

### Avec bouton d'appel piétons

![](img/feu_tricolore-2.png)

Note: Problème : et si un gamin facétieux appuie sur le bouton à chaque
fois que le feu passe au vert ?

<!--vertical-->

### Avec temps minimum au vert

![](img/feu_tricolore-3.png)

Note: Problème : si un piéton appelle dans l'état VERT, l'appel n'est
pas pris en compte.

<!--vertical-->

### Avec bouton d'appel toujours sensible

![](img/feu_tricolore-4.png)

Note: La transition VERT\_APPEL → ORANGE peut être instantanée
(l'état VERT\_APPEL est alors éphémère)  
Le diagramme d'état sert à affiner le cahier des charges.

<!--horizontal-->

## Servo qui scanne

Utilisation de la bibliothèque Servo :

```arduino
#include <Servo.h>

const uint8_t broche_servo = 4;
const int pas_servo = 2;          // pas de 2 degrés
const uint32_t duree_pause = 20;  // pause entre deux pas

Servo mon_servo;

void setup() {
    mon_servo.attach(broche_servo);
}

void loop() {
    for (int position = 2; position <= 180; position += pas_servo) {
        mon_servo.write(position);
        delay(duree_pause);
    }
    for (int position = 178; position >= 0; position -= pas_servo) {
        mon_servo.write(position);
        delay(duree_pause);
    }
}
```

Note: Il y a 360 états.

<!--vertical-->

### Servo qui scanne – non bloquant

```arduino
void loop() {
    if (millis() - temps_dernier_pas >= duree_pause) {
        temps_dernier_pas += duree_pause;

        // Calculer le prochain état.
        switch (direction) {
            case AVANCE:
                position += pas_servo;
                if (position >= 180) direction = RECULE;
                break;
            case RECULE:
                position -= pas_servo;
                if (position <= 0) direction = AVANCE;
                break;
        }

        // Appliquer l'état calculé.
        mon_servo.write(position);
    }
}
```

Note: **dans examples/**  
L'état est dans **deux** variables d'état (lesquelles ?)

<!--horizontal-->

## Moteur de portail

Programmer cet automate.

![](img/portail.png)

Note: Et si on appuie sur le bouton quand le portail est en mouvement ?

<!--horizontal-->

## Debounce

Commuter une LED par appui sur un bouton.

Exemple de code problématique :

```arduino
void loop() {
    static int etat_LED = LOW;
    static int ancien_etat_bouton = LOW;
    int etat_bouton = digitalRead(broche_bouton);

    // Détection de l'appui sur le bouton.
    if (ancien_etat_bouton == LOW && etat_bouton == HIGH) {
        etat_LED = etat_LED==HIGH ? LOW : HIGH;
        digitalWrite(led_pin, etat_LED);
    }
    ancien_etat_bouton = etat_bouton;
}
```

<!--vertical-->

## Solution par machine à états

![](img/debounce.png)

<!--vertical-->

## Solution avec une bibliothèque

```arduino
#include <Bounce2.h>

const int broche_bouton = 2;
const int broche_led = 8;

Bounce bouton;  // création de l'objet Bounce représentant le bouton

void setup() {
    bouton.attach(broche_bouton, INPUT);
    pinMode(broche_led, OUTPUT);
}

void loop() {
    static int etat_LED = LOW;

    bouton.update();  // toujours appeler ça régulièrement

    // Quand on appuie sur le bouton, changer l'état de la LED.
    if (bouton.rose()) {
        etat_LED = etat_LED==HIGH ? LOW : HIGH;
        digitalWrite(broche_led, etat_LED);
    }
}
```

Note: **dans examples/**  

<!--horizontal-->

## Lire un port série

Lecture bloquante :

```arduino
void loop() {
    // Attendre des données sur le port série.
    while (Serial.available() == 0)
        { /* attendre... */ }

    // Lire une commande et l'exécuter.
    String commande = Serial.readString();
    interpret(commande);
}
```

Note: readString() est aussi bloquant → timeout.

<!--vertical-->

### Solution non-bloquante

```arduino
void loop() {
    static char tampon[80];        // stockage des caractères reçus
    static size_t pos_tampon = 0;  // nombre de caractères reçus

    if (Serial.available()) {
        char c = Serial.read();         // nouveau caractère reçu
        if (c == '\n') {                // fin de ligne
            tampon[pos_tampon] = '\0';  // terminer la chaîne
            interprete(tampon);
            pos_tampon = 0;             // préparer la prochaine lecture
        } else if (pos_tampon < sizeof tampon - 1) {
            tampon[pos_tampon++] = c;   // ajouter au tampon
        }
    }
}
```

Voir aussi:
* [Reading Serial on the Arduino][majenko], par Majenko
* [Simple Arduino command line interpreter][gist], par Edgar

[majenko]: https://majenko.co.uk/blog/reading-serial-arduino
[gist]: https://gist.github.com/edgar-bonet/607b387260388be77e96

Note: **dans examples/**  
On profite pour éviter String.
